LazyInteger = __int__ ->
  '''An object that can be converted into an integer on demand.

    type LazyInteger { __int__ :: -> int }

  '''
  call $ subclass object


Generator.get_freevar = name ~>
  '''Fetch the index of a free variable.

    `cellvars` and `freevars` share the index space, so
    this needs to be deferred until compilation time.

  '''
  LazyInteger $ _ i: (@freevars name) -> len @cellvars + 1


Generator.load = name ~>
  '''Push the value assigned to some name onto the value stack.

    load :: Name -> a

  '''
  v = name.value

  if v in @cellvars => @add_opcode +1 'LOAD_DEREF'  $ @cellvars v
     v in @varnames =>
       (@f_locals !! v).add (len @bytecode, dis.opmap !! 'LOAD_DEREF')
       @add_opcode +1 'LOAD_FAST' $ @varnames v
     v in @enclosed => @add_opcode +1 'LOAD_DEREF'  $ @get_freevar v
     @slowlocals    => @add_opcode +1 'LOAD_NAME'   $ @names v
     otherwise      => @add_opcode +1 'LOAD_GLOBAL' $ @names v


Generator.store = name ~>
  '''Store the value on top of the stack in a variable.

    store :: Name -> a

  '''
  v = name.value

  if v in @cellvars => @add_opcode -1 'STORE_DEREF' $ @cellvars    v
     v in @enclosed => @add_opcode -1 'STORE_DEREF' $ @get_freevar v
     @slowlocals    => @add_opcode -1 'STORE_NAME'  $ @names v
     otherwise      =>
       (@f_locals !! v).add (len @bytecode, dis.opmap !! 'STORE_DEREF')
       @add_opcode -1 'STORE_FAST' $ @varnames v


Generator.load_closure = name ~>
  '''Load a cell object referencing some variable.

    load_closure :: Name -> a

  '''
  v = name.value

  for (i, c) in @f_locals !! v =>
    @bytecode !! i = c, @cellvars v

  @add_opcode +1 'LOAD_CLOSURE' $ @cellvars.get v $ @get_freevar v
