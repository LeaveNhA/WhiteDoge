#
# a if b else c
#
# Obvious ternary conditional.
#
PREFIX !! 'else' = (self f args) ->
  cond, otherwise = ensure f args 2 2
  then, cond = parse.syntax.binary_op 'if' cond $ bind parse.syntax.error 'first goes `if`, then `else`'

  ptr = self.loadop 'POP_JUMP_IF_FALSE' cond delta: 0
  jmp = self.loadop 'JUMP_FORWARD'      then delta: 0

  ptr!
  self.load otherwise
  jmp!


#
# switch
#   condition1 = when_condition1_is_true
#   ...
#   conditionN = when_conditionN_is_true
#
switch_s = cases -> list $ map
  bind parse.syntax.binary_op '=' on_error:
    bind parse.syntax.error '`switch` expected (condition = action) pairs'
  cases


#
# Evaluate the first action assigned to a true condition.
# `if-elif` is probably a better equivalent than `switch-case`.
#
PREFIX !! 'switch' = (self f cases) ->
  jumps = list $ map (cond, action) -> (
    next = self.loadop 'POP_JUMP_IF_FALSE' cond delta: 0
    end  = self.loadop 'JUMP_FORWARD'    action delta: 0
    next!
    end
  ) $ switch_s cases

  self.load None  # in case nothing matched
  list $ map call jumps
