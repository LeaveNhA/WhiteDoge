#
# inherit: ... block
#
# Create a class with `block` as its body.
# All arguments but the last one are passed straight to the `__build_class__`
# function.
#
PREFIX !! 'inherit' = (self f args) ->
  *args, body = ensure f args

  # __build_class__ will also need a `dict -> cell` function.
  name = self.child_name '<class>'
  code = CodeGenerator name True (list' '__locals__') cell: self
  code.cellhook = code -> code.freevars !! '__class__'
  code.cellvars !! '__class__'
  code.varnames !! '__class__'

  # The argument, __locals__, is what we need to write attributes to.
  code.append 'LOAD_FAST'    '__locals__'   delta:  1
  code.append 'STORE_LOCALS'                delta: -1
  code.append 'LOAD_NAME'    '__name__'     delta:  1
  code.append 'STORE_NAME'   '__module__'   delta: -1
  code.append 'LOAD_CONST'   code.qualname  delta:  1
  code.append 'STORE_NAME'   '__qualname__' delta: -1
  code.loadop 'POP_TOP'      body           delta:  0
  # The return value is a __class__ cell, if any.
  # Python compiler returns None instead if there are no instance methods.
  code.append 'LOAD_CLOSURE' '__class__'    delta:  1
  code.append 'RETURN_VALUE'                delta: -1

  self.loadop 'LOAD_BUILD_CLASS' delta: 1
  self.make_function code tuple! dict!
  self.nativecall code.name args 2 False
